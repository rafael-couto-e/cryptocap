plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id('io.gitlab.arturbosch.detekt').version('1.17.1')
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "br.eti.rafaelcouto.cryptocap"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "BASE_URL", "\"https://pro-api.coinmarketcap.com/v1/cryptocurrency/\""
        buildConfigField "String", "API_KEY", "\"2ec8b9db-3efc-483e-a736-d5365813a50e\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/sharedTest"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/sharedTest"
        }
    }
}

android.sourceSets {

}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

check.dependsOn ktlint, detekt

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

detekt {
    buildUponDefaultConfig = true
    allRules = false
    config = files("$projectDir/detekt.yml")

    reports {
        html.enabled = true
        xml.enabled = false
        txt.enabled = true
        sarif.enabled = false
    }
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"

    // Android
    implementation "androidx.core:core-ktx:$versions.corektx"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"

    // Material and UI
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraint"
    implementation "androidx.recyclerview:recyclerview:$versions.recyclerview"
    implementation "androidx.cardview:cardview:$versions.cardview"
    implementation "androidx.paging:paging-runtime-ktx:$versions.paging"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$versions.swiperefresh"
    implementation "com.github.bumptech.glide:glide:$versions.glide"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    // DI
    implementation "io.insert-koin:koin-core:$versions.koin"
    implementation "io.insert-koin:koin-android:$versions.koin"

    // Network
    implementation "com.squareup.okhttp3:okhttp:$versions.okhttp"
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"

    // Room
    kapt "androidx.room:room-compiler:$versions.room"
    implementation "androidx.room:room-runtime:$versions.room"
    implementation "androidx.room:room-ktx:$versions.room"

    // Verification
    ktlint("com.pinterest:ktlint:$versions.ktlint") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }

    // Unit tests
    testImplementation "junit:junit:$versions.junit"
    testImplementation "androidx.test.ext:truth:$versions.truth"
    testImplementation "android.arch.core:core-testing:$versions.coretesting"
    testImplementation "io.mockk:mockk:$versions.mockk"
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okhttp"
    testImplementation "io.insert-koin:koin-test:$versions.koin"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$versions.coroutines"
    testImplementation "androidx.room:room-testing:$versions.room"

    // UI Tests
    androidTestImplementation "androidx.test.ext:junit:$versions.androidjunit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
    androidTestImplementation "androidx.test.ext:truth:$versions.truth"
}
